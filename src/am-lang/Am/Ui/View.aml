
namespace Am.Ui 
{
    class Graphics {
        import Am.Lang
        var xOffset: Short
        var yOffset: Short

        fun translate(x: Short, y: Short) {
            this.xOffset = this.xOffset + x
            this.yOffset = this.yOffset + y
        }

        abstract fun setForegroundPen(pen: UByte)
        abstract fun setBackgroundPen(pen: UByte)
        abstract fun drawLine(x: Short, y: Short, x2: Short, y2: Short)
        abstract fun fillRect(x: Short, y: Short, x2: Short, y2: Short)
        abstract fun eraseRect(x: Short, y: Short, x2: Short, y2: Short)
    }

    interface ViewContext {
//        private var window: Window
//private var window: Window
        fun getWindow(): Window

        // Keep information about Window/Screen/Gadget Context etc.
    }

    native class ViewContextGraphics(var viewContext: ViewContext) : Graphics {
        import Am.Lang
        private fun getWindow(): Window {
            return this.viewContext.getWindow()
        }
        native override fun setForegroundPen(pen: UByte)
        native override fun setBackgroundPen(pen: UByte)
        native override fun drawLine(x: Short, y: Short, x2: Short, y2: Short)
        native override fun eraseRect(x: Short, y: Short, x2: Short, y2: Short)
        native override fun fillRect(x: Short, y: Short, x2: Short, y2: Short)

    }


    class ViewContextImpl : ViewContext {
        import Am.Lang

        private var windowRef: Weak<Window> = new Weak<Window>()

        fun getWindow(): Window {
            return this.windowRef.get()
        }

        fun setWindow(window: Window) {
            this.windowRef.set(window)
        }
    }

    class View {
        import Am.Lang
        private var viewContext: ViewContext
        var x: Short
        var y: Short
        var width: UShort
        var height: UShort
        var padding: Padding = new Padding(0S, 0S, 0S, 0S)

        fun minWidth(): Short {
            return 0S
        }

        fun minHeight(): Short {
            return 0S
        }

        fun growX(): UByte {
            return 0UB
        }

        fun growY(): UByte {
            return 0UB
        }

        fun innerX(): Short {
            return this.x + this.padding.left
        }

        fun innerY(): Short {
            return this.y + this.padding.top
        }

        fun innerWidth(): Short {
            return (this.width.toShort() - (this.padding.left + this.padding.right))
        }

        fun innerHeight(): Short {
            return this.height.toShort() - (this.padding.top + this.padding.bottom)
        }

        fun setPadding(padding: Short) {
            this.padding.left = padding
            this.padding.top = padding
            this.padding.right = padding
            this.padding.bottom = padding
        }

        fun setPadding(horizontal: Short, vertical: Short) {
            this.padding.left = horizontal
            this.padding.top = vertical
            this.padding.right = horizontal
            this.padding.bottom = vertical
        }

        fun getViewContext(): ViewContext {
            return this.viewContext
        }

        fun getWindow(): Window {
            return this.viewContext.getWindow()
        }

        fun onAttach(viewContext: ViewContext) {
            if (this.viewContext != null) {
                throw new Exception("View already attached")
            }
            this.viewContext = viewContext
        }

        fun onDetach() {
            this.viewContext = null
        }

        fun paint(graphics: Graphics) {

        }

        fun paintAll(graphics: Graphics) {
            var ix = this.innerX()
            var iy = this.innerY()
            ("Inner pos " + ix.toString()).println()
            graphics.translate(ix, iy)
            this.paint(graphics)
            graphics.translate(-ix, -iy)
        }

        fun layout(x: Short, y: Short, width: UShort, height: UShort) {
            this.x = x
            this.y = y
            this.width = width
            this.height = height
            ("Layout " + x.toString() + "," + y.toString() + "," + width.toString() + "," + height.toString()).println()
        }

        fun contains(x: Short, y: Short): Bool {
//            ("contains " + x.toString() + "," + y.toString() + " in " + this.width.toString() + ", " + this.height.toString()).println()
            return (x >= 0S) && (x < this.width) && (y >= 0S) && (y < this.height)
        }

        fun findViewAt(x: Short, y: Short): View {
            if (this.contains(x, y)) {
                return this
            }
            return null
        }

        // returns true if consumed
        fun onMouseClick(button: UByte, x: Short, y: Short): Bool {
//            ("View click: " + button.toString() + " " + x.toString() + " " + y.toString()).println()

            if (this.contains(x, y)) {
                return true
            }
            return false
        }


    }

    class ViewGroup : View {
        import Am.Lang
        import Am.Collections

        private var children: List<View> = List<View>.newList<View>()
        
        var font: Font

        fun getChildren() {
            return this.children
        }

        fun addChild(view: View) {
            this.children.add(view)
            if (this.viewContext != null) {
                view.onAttach(this.viewContext)
            }
        }

        fun removeChild(view: View) {
            this.children.remove(view)
            view.onDetach()
        }

        fun paintChildren(graphics: Graphics) {
            each(this.children, c) {
                c.paintAll(graphics)
            }
        }

        override fun paintAll(graphics: Graphics) {
            var ix = this.innerX()
            var iy = this.innerY()
            ("inner x for viewgroup: " + ix.toString()).println()
            graphics.translate(ix, iy)
            this.paint(graphics)
            this.paintChildren(graphics)
            graphics.translate(-ix, -iy)
        }

        fun layoutChildren() {
            each(this.children, c) {
                // Do nothing
            }
        }

        override fun layout(x: Short, y: Short, width: UShort, height: UShort) {
            base.layout(x, y, width, height)
            this.layoutChildren()
        }

        override fun findViewAt(x: Short, y: Short): View {
            if (this.contains(x, y)) {
                each(this.children, c) {
                    var lx = x - c.x
                    var ly = y - c.y

                    var v = c.findViewAt(lx, ly)
                    if (v != null) {
                        return v
                    }
                }
                return this
            }
            return null
        }

        override fun onMouseClick(button: UByte, x: Short, y: Short): Bool {
            if (this.contains(x, y)) {
                each(this.children, c) {
                    var lx = x - c.x
                    var ly = y - c.y

                    var consumed = c.onMouseClick(button, lx, ly)
                    if (consumed) {
                        return consumed
                    }
                }
            }
            return false
        }


    }

    class VStack(var spacing: Short) : ViewGroup {
        import Am.Lang
        import Am.Collections

        override fun layoutChildren() {
            var left = 0S
            var y = 0S
            var height = this.innerHeight()
            var minHeight = this.innerMinHeight()
            var unusedHeight = height - minHeight
            var remainingHeight = restHeight
            var sumGrowY = this.sumGrowY()
            var innerWidth = this.innerWidth() 
            ("inner width VStack: " + innerWidth.toString() + " vs " + this.width.toString()).println()

            each(this.children, c) {
                var ch = c.minHeight() + ((c.growY().toInt() * unusedHeight.toInt()) / sumGrowY.toInt()).toShort()
                c.layout(left, y, innerWidth.toUShort(), ch.toUShort())
                y = y + ch + this.spacing
            }
        }

        fun innerMinHeight(): Short {
            var h = 0S
            each(this.children, c) {
                if (h > 0S) {
                    h = h + this.spacing
                }
                h = h + c.minHeight()
            }
            return h
        }

        override fun minHeight(): Short {
            return this.innerMinHeight() + this.padding.top + this.padding.bottom
        }

        fun innerMinWidth(): Short {
            var minW = 0S
            each(this.children, c) {
                var cMinW = c.minWidth()
                if (cMinW > minW) {
                    minW = cMinW
                }
            }
            return minW
        }

        override fun minWidth(): Short {
            return this.innerMinWidth() + this.padding.left + this.padding.right
        }

        fun sumGrowY(): Short {
            var grow = 0S
            each(this.children, c) {
                grow = grow + c.growY().toShort()
            }
            return grow
        }

    }

    class HStack : ViewGroup {
        import Am.Lang
        import Am.Collections

        override fun layoutChildren() {
            var x = 0S
            var y = 0S
            var width = this.width
            var childWidth = width / this.children.size.toUShort()

            each(this.children, c) {
                c.layout(x, y, childWidth, this.height)
                x = x + childWidth.toShort()
            }
        }

    }

    class Font(private var name: String) {
        import Am.Lang

        fun getName(): String {
            return this.name
        }
    }

    native class GadgetViews {
        import Am.Lang

        native static fun layoutNative(gadgetView: GadgetView, x: Short, y: Short, width: UShort, height: UShort)
    }

    class GadgetView : View {
        import Am.Lang

        override fun layout(x: Short, y: Short, width: UShort, height: UShort) {
            base.layout(x, y, width, height)
            GadgetViews.layoutNative(this, x, y, width, height)
        }
    }

    native class Button : GadgetView {
        import Am.Lang

        private native fun attachButton(window: Window)
        private native fun detachButton(window: Window)

        override fun onAttach(viewContext: ViewContext) {
            base.onAttach(viewContext)
            this.attachButton(viewContext.getWindow())
        }

        override fun onDetach() {
            this.detachButton(this.viewContext.getWindow())
            base.onDetach()
        }
    }

    class TestButton(var grow: UByte) : View {
        import Am.Lang

        override fun growX(): UByte {
            return this.grow
        }

        override fun growY(): UByte {
            return this.grow            
        }

        override fun minHeight(): Short {
            return 30S
        }

        override fun minWidth(): Short {
            return 30S
        }

        override fun paint(graphics: Graphics) {
            base.paint(graphics)

            var x1 = 0S
            var x2 = this.innerWidth() - 1S
            var y1 = 0S
            var y2 = this.innerHeight() - 1S

            ("Paint " + x1.toString() + " to " + x2.toString()).println()

            graphics.setForegroundPen(3UB)
            graphics.fillRect(x1, y1, x2, y2)
            graphics.setForegroundPen(1UB)

            graphics.drawLine(x1, y1, x2, y1)
            graphics.drawLine(x2, y1, x2, y2)
            graphics.drawLine(x1, y2, x2, y2)
            graphics.drawLine(x1, y1, x1, y2)
        }

        override fun onMouseClick(button: UByte, x: Short, y: Short): Bool {
//            ("Button click: " + button.toString() + " " + x.toString() + " " + y.toString()).println()

            if (this.contains(x, y)) {
//                "Button clicked".println()
                return true
            }
            return false
        }

    }
}

