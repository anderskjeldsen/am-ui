
namespace Am.Ui 
{
    class VStack(var spacing: Short) : ViewGroup {
        import Am.Lang
        import Am.Collections

        override fun layoutChildren() {
            var left = this.padding.left
            var y = this.padding.top
            var height = this.innerHeight()
            var minHeight = this.innerMinHeight()

            var unusedHeight = Short.max(height - minHeight, 0S)
            var remainingHeight = unusedHeight

            var sumGrowY = this.sumGrowY()

            var innerWidth = this.innerWidth() 

            each(this.children, c) {
                var ch = c.minHeight() + ((c.growY.toInt() * unusedHeight.toInt()) / sumGrowY.toInt()).toShort()
                c.layout(left, y, innerWidth.toUShort(), ch.toUShort())
                y = y + ch + this.spacing
            }
        }

        fun innerMinHeight(): Short {
            var h = 0S
            each(this.children, c) {
                if (h > 0S) {
                    h = h + this.spacing
                }
                h = h + c.minHeight()
            }
            return h
        }

        override fun minHeight(): Short {
            return this.innerMinHeight() + this.padding.top + this.padding.bottom
        }

        fun innerMinWidth(): Short {
            var minW = 0S
            each(this.children, c) {
                var cMinW = c.minWidth()
                if (cMinW > minW) {
                    minW = cMinW
                }
            }
            return minW
        }

        override fun minWidth(): Short {
            return this.innerMinWidth() + this.padding.left + this.padding.right
        }

        fun sumGrowY(): Short {
            var grow = 0S
            each(this.children, c) {
                grow = grow + c.growY.toShort()
            }
            if (grow == 0) {
                grow = 1S
            }
            return grow
        }

    }


}

