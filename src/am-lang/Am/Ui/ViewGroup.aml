
namespace Am.Ui 
{
    class ViewGroup : View {
        import Am.Lang
        import Am.Collections

        private var children: List<View> = new List<View>()
        
        var font: Font

        fun getChildren(): List<View> {
            return this.children
        }

        fun addChild(view: View) {
            this.children.add(view)
            if (this.getViewContext() != null) {
                view.onAttach(this.getViewContext())
            }
        }

        fun removeChild(view: View) {
            this.children.remove(view)
            view.onDetach()
        }

        fun removeChildren() {
            each(this.children, c) {
                c.onDetach()
            }
            var newList = List<View>.newList<View>()
            this.children = newList
        }

        override fun onAttach(viewContext: ViewContext) {
            base.onAttach(viewContext)
            each(this.children, c) {
                c.onAttach(viewContext)
            }
        }

        override fun onDetach() {
            base.onDetach()
            each(this.children, c) {
                c.onDetach()
            }
        }


        fun paintChildren(graphics: Graphics) {
            each(this.children, c) {
                c.paintAll(graphics)
            }
        }

        override fun paintAll(graphics: Graphics) {
            var b1 = graphics.shouldRepaintAll()
            var b2 = this.getRepaintRequested()
            if (b1 || b2) {
                var ix = this.x + this.margin.left
                var iy = this.y + this.margin.top
                
                // Set clipping area to this view's bounds (excluding margins)
                var viewWidth = this.width.toShort() - this.margin.left - this.margin.right
                var viewHeight = this.height.toShort() - this.margin.top - this.margin.bottom
                
                var currentClip = graphics.getCurrentClipRect()
                var viewClipRect: ClipRect = currentClip.createInnerClipRect(ix, iy, viewWidth.toUShort(), viewHeight.toUShort())
                graphics.pushClipRect(viewClipRect)                
                graphics.beginPainting(viewClipRect)

                graphics.translate(ix, iy)
                this.paint(graphics)
                graphics.translate(-ix, -iy)

                graphics.endPainting()
                graphics.popClipRect()
            }
            this.markAsPainted()
            
            // Paint children with proper translation
            var ix = this.x
            var iy = this.y
            graphics.translate(ix, iy)            
            this.paintChildren(graphics)
            graphics.translate(-ix, -iy)
        }

        open fun layoutChildren() {
            each(this.children, c) {
                // Do nothing
            }
        }

        override fun layout(x: Short, y: Short, width: UShort, height: UShort) {
            base.layout(x, y, width, height)
            this.layoutChildren()
        }

        override fun findViewAt(x: Short, y: Short): View {
            if (this.contains(x, y)) {
                each(this.children, c) {
                    var lx = x - c.x
                    var ly = y - c.y

                    var v = c.findViewAt(lx, ly)
                    if (v != null) {
                        return v
                    }
                }
                return this
            }
            return null
        }

        override fun onKeyboardEvent(type: KeyboardEventType, keyCode: UShort, keyChar: UShort): Bool {
            each(this.children, c) {
                var consumed = c.onKeyboardEvent(type, keyCode, keyChar)
                if (consumed) {
                    return consumed
                }
            }
            return false
        }


        override fun onMouseEvent(type: MouseEventType, button: MouseButton, x: Short, y: Short): Bool {
            if (this.contains(x, y)) {
                each(this.children, c) {
                    var lx = x - c.x 
                    var ly = y - c.y

                    var consumed = c.onMouseEvent(type, button, lx, ly)
                    if (consumed) {
                        return consumed
                    }
                }
            }
            return false
        }

        fun setupGroup(i: (it: ViewGroup) => ViewGroup) : ViewGroup {
            i(this)
            return this
        }
    }

}

